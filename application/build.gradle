buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
        classpath 'com.bmuschko:gradle-docker-nodejs-plugin:0.1'
    }
}

apply plugin: 'com.moowork.node'
apply plugin: 'com.bmuschko.docker-nodejs-application'

version = '1.0.0'
group = 'com.bmuschko'

node {
    version = '9.8.0'
    download = true
}

task helloWorld(type: NodeTask) {
    dependsOn npmInstall
    script = file('src/node')
}

docker {
    registryCredentials {
        username = getConfigurationProperty('DOCKER_USERNAME', 'docker.username')
        password = getConfigurationProperty('DOCKER_PASSWORD', 'docker.password')
        email = getConfigurationProperty('DOCKER_EMAIL', 'docker.email')
    }
    
    nodeJsApplication {
        tag = "bmuschko/nodejs-hello-world:$project.version"
    }
}

String getConfigurationProperty(String envVar, String sysProp) {
    System.getenv(envVar) ?: project.findProperty(sysProp)
}